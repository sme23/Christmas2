EventPointerTable(0x7, PointerList)

#define VillageJustVisitedFlag 120

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

ALIGN 32
TurnBasedEvents:
END_MAIN

ALIGN 32
CharacterBasedEvents:
CharacterEventBothWays(7,DorcasRecruit,Oswin,Dorcas)
CharacterEventBothWays(8,GuyRecruit,Matthew,Guy)
END_MAIN

ALIGN 32
MiscBasedEvents:
AFEV 0 GlobalPostVillageEventCheck VillageJustVisitedFlag
AFEV 0 CheckIfWon 0
CauseGameOverIfLordDies
END_MAIN

ALIGN 32
LocationBasedEvents:
Village(9,Village1,0,9)
Village(10,Village2,5,7)
Village(11,Village3,10,2)
Village(12,Village4,12,2)
Village(13,Village5,12,5)
Village(14,Village6,10,8)
Village(15,Village7,12,8)
Village(16,Village8,12,11)
Village(17,Village9,14,11)
Village(18,Village10,16,11)
Village(19,Village11,17,7)
Village(20,Village12,18,2)
Village(21,Village13,22,5)
Village(22,Village14,22,10)
Village(23,Village15,24,10)
Armory(ArmoryList,7,3)
Vendor(VendorList,1,4)
Chest(BlueGem,8,12)
Chest(RedGem,19,11)
Chest(WrathManual,24,7)
Chest(SolManual,17,4)
END_MAIN

ALIGN 32
Dunno1:
END_MAIN

ALIGN 32
Dunno2:
END_MAIN

ALIGN 32
Dunno3:
END_MAIN

ALIGN 32
Tutorial:
END_MAIN

ALIGN 4
Units1:
UNIT Hector GreatLord 0 Level(5,Ally,0) [3,0] 0x0 0x0 1 REDA4R3 [Armads,Tomahawk,Elixir] NoAI
UNIT Oswin General 0 Level(5,Ally,0) [3,0] 0x0 0x0 1 REDA3R2 [Spear,HandAxe,SilverSword] NoAI
UNIT Matthew Rogue 0 Level(5,Ally,0) [4,0] 0x0 0x0 1 REDA4R2 [IronSword,WhiteGem] NoAI
UNIT Serra Valkyrie 0 Level(5,Ally,0) [4,0] 0x0 0x0 1 REDA5R1 [Divine,Entrap,Mend] NoAI
UNIT

EnemyUnits: //Raven
UNIT Raven Hero 0 Level(10,Enemy,False) [22,0] 0x0 0x0 1 REDA23R2 [BraveSword,SilverAxe,Elixir] NeverMoveAI

UNIT

EnemyUnits2: //Guy
UNIT Guy Swordmaster 0 Level(5,Enemy,False) [9,12] 0x0 0x0 1 REDA9R10 [LevinSword,KillingEdge,Vulnerary] AttackInRangeAI
UNIT PrologueBandit Bishop 0 Level(2,Enemy,True) [10,12] 0x0 0x0 1 REDA14R8 [Divine] AttackInRangeAI
UNIT PrologueBandit Hero 0 Level(3,Enemy,True) [11,12] 0x0 0x0 1 REDA15R12 [SilverSword] AttackInRangeAI
UNIT

EnemyUnits3: //Initial
UNIT PrologueBandit Sage 0 Level(2,Enemy,True) [3,7] 0x0 0x0 0x0 0x1 [Elfire] AttackInRangeAI
UNIT PrologueBandit Swordmaster_F 0 Level(2,Enemy,True) [9,5] 0x2 0x0 1 REDA9R5 [LightBrand] AttackInRangeAI
UNIT PrologueBandit Warrior 0 Level(1,Enemy,True) [4,12] 0x0 0x0 1 REDA4R12 [SteelAxe,SteelBow] AttackInRangeAI
UNIT PrologueBandit Hero 0 Level(4,Enemy,True) [0,11] 0x0 0x0 1 REDA0R11 [Tomahawk,RedGem] AttackInRangeAI
UNIT PrologueBandit Sniper 0 Level(2,Enemy,True) [6,9] 0x0 0x0 1 REDA6R9 [SilverBow] AttackInRangeAI
UNIT

EnemyUnits4: //Dorcas
UNIT Dorcas Warrior 0 Level(5,Enemy,False) [23,0] 0x0 0x0 1 REDA14R3 [EmeraldAxe,SteelBow,Vulnerary] AttackInRangeAI
UNIT PrologueBandit MageKnight_F 0 Level(3,Enemy,True) [24,0] 0x0 0x0 1 REDA16R2 [Thunder] AttackInRangeAI
UNIT PrologueBandit Paladin 0 Level(3,Enemy,True) [23,0] 0x0 0x0 1 REDA13R6 [SteelSword] AttackInRangeAI
UNIT

EnemyUnits5: //Lucius
UNIT Lucius Bishop_F 0 Level(10,Enemy,False) [23,0] 0x0 0x0 1 REDA19R4 [Aura,Elixir] AttackInRangeAI
UNIT PrologueBandit Druid 0 Level(3,Enemy,True) [24,0] 0x0 0x0 1 REDA20R2 [Flux,RedGem] AttackInRangeAI
UNIT PrologueBandit Falcoknight 0 Level(4,Enemy,True) [24,0] 0x0 0x0 1 REDA23R5 [SteelLance] AttackInRangeAI
UNIT PrologueBandit WyvernKnight 0 Level(3,Enemy,True) [23,0] 0x0 0x0 1 REDA23R0 [SteelLance] AttackInRangeAI
UNIT

EnemyUnits6: //Priscilla
UNIT Priscilla Valkyrie 0 Level(10,Enemy,False) [23,0] 0x0 0x0 1 REDA19R9 [Divine,Sleep] AttackInRangeAI
UNIT PrologueBandit WyvernLord 0 Level(4,Enemy,True) [24,0] 0x0 0x0 1 REDA24R9 [SilverSword] AttackInRangeAI
UNIT PrologueBandit Berserker 0 Level(2,Enemy,True) [23,0] 0x0 0x0 1 REDA21R11 [KillerAxe,BlueGem] AttackInRangeAI
UNIT PrologueBandit WyvernLord_F 0 Level(3,Enemy,True) [24,0] 0x0 0x0 1 REDA17R8 [SteelLance] AttackInRangeAI
UNIT

//UNIT PrologueBandit Druid 0 Level(4,Enemy,True) [,] 0x0 0x0 0x0 0x1 [Nosferatu] AttackInRangeAI
//UNIT PrologueBandit Warrior 0 Level(4,Enemy,True) [,] 0x2 0x0 0x0 0x1 [HandAxe] AttackInRangeAI
//UNIT PrologueBandit Sniper 0 Level(5,Enemy,True) [,] 0x2 0x0 0x0 0x1 [KillerBow] AttackInRangeAI
//UNIT PrologueBandit Paladin 0 Level(5,Enemy,True) [,] 0x0 0x0 0x0 0x1 [SilverSword] AttackInRangeAI
//UNIT PrologueBandit GreatKnight 0 Level(5,Enemy,True) [,] 0x0 0x0 0x0 0x1 [SteelSword,SteelLance,SteelAxe] AttackInRangeAI
UNIT


Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:


LOMA 1
FADU 10

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW OpeningText1
TEXTEND

//display mug at mid left position
SHORT 0x1E21 RavenMug

//positioned to top of screen
SVAL 0xB 0x00060024
TUTORIALTEXTBOXSTART
TEXTSHOW OpeningText2
TEXTEND

//clear all mugs
SHORT 0x1E20 (-3)

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW OpeningText3
TEXTEND

CenterTutorialTextBox
TEXTSTART
TEXTSHOW OpeningText4
TEXTEND

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW OpeningText5
TEXTEND

Text(OpeningText6) //clears face

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW OpeningText7
TEXTEND




//display Serra mug
SHORT 0x1E21 NormalSerraMug

SVAL 0xB 0x00060024
TUTORIALTEXTBOXSTART
TEXTSHOW OpeningText81
TEXTEND

SHORT 0x1E20 (-3)
STAL 10
//display Oswin mug
SHORT 0x1E21 NormalOswinMug

SVAL 0xB 0x00060024
TUTORIALTEXTBOXSTART
TEXTSHOW OpeningText82
TEXTEND

SHORT 0x1E20 (-3)
STAL 10
//display Matthew mug
SHORT 0x1E21 NormalMatthewMug

SVAL 0xB 0x00060024
TUTORIALTEXTBOXSTART
TEXTSHOW OpeningText83
TEXTEND

//clear all mugs
SHORT 0x1E20 (-3)

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW OpeningText84
TEXTEND

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW OpeningText9
TEXTEND

FADI 10
LOMA 0
CAM1 [0,0]
FADU 10

TILECHANGE 19
SOUN 0xB1
STAL 10

LOAD1 0x1 Units1
ENUN

Text(OpeningText10)

CAM1 [24,0]
STAL 10
TILECHANGE 20
SOUN 0xB1
STAL 10

//load in cutscene enemy bosses 
LOAD1 0x1 CutsceneRavenCrew
ENUN

Text(OpeningText11)

MOVE 0 Raven [24,0]
MOVE 0 Priscilla [23,0]
MOVE 0 Lucius [23,0]
ENUN
DISA Raven
DISA Priscilla
DISA Lucius

CAM1 [0,0]
STAL 10

FADI 10
LOAD1 0x1 EnemyUnits3 //keep this as the starting enemy group
ENUN
FADU 10

Text(OpeningText12)

//initialize win counter
COUNTER_SET 0 0

NoFade
ENDA

CutsceneRavenCrew:
UNIT Raven Hero 0 Level(0,Enemy,0) [24,0] 0x0 0x0 0x1 REDA23R4 NoItems NoAI
UNIT Priscilla Valkyrie 0 Level(0,Enemy,0) [23,0] 0x0 0x0 0x1 REDA21R3 NoItems NoAI
UNIT Lucius Bishop_F 0 Level(0,Enemy,0) [23,0] 0x0 0x0 0x1 REDA21R5 NoItems NoAI
UNIT

EndingScene:

MUSC Victory

Text(EndingText1)

SetBackground(1)

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW EndingText2
TEXTEND

Text(EndingText3)

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW EndingText4
TEXTEND

Text(EndingText5)

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW EndingText6
TEXTEND

Text(EndingText7)

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW EndingText8
TEXTEND

SetBackground(ChristmasCG)

SVAL 0xB 0x00060024
TUTORIALTEXTBOXSTART
TEXTSHOW EndingText9
TEXTEND

FADI 1

LOMA 1
FADU 10

MUSC 0x7FFF

STAL 10

MUSC 53

Text(EndingTextFinal)

MUSC DrumrollPleaseSong

CALL CalculateScore

ASMC 0x85C71 0x381F5 0x37CF9 //show records screen
STAL 120
EVBIT_F 0x2
MNTS 0x1 //return to title screen
NoFade
ENDA


ALIGN 4
DorcasRecruit:
MUSS Comrades
Text(RecruitDorcasText)
CUSA Dorcas
MURE 5
NoFade
ENDA

ALIGN 4
GuyRecruit:
MUSS Comrades
Text(RecruitGuyText)
CUSA Guy
MURE 5
NoFade
ENDA

ALIGN 4
Village1:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
CALL EndingScene
ENUT VillageJustVisitedFlag 
NoFade
ENDA

ALIGN 4
Village2:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

ALIGN 4
Village3:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

Village4:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

Village5:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

Village6:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

Village7:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

Village8:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

Village9:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

Village10:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

Village11:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

Village12:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

Village13:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

Village14:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

Village15:
SetBackground(1)
CALL RandomVillageTextEvent
COUNTER_INC 0
ENUT VillageJustVisitedFlag 
NoFade
ENDA

CheckIfWon:
COUNTER_CHECK 0
SVAL 1 15
BNE 1 1 0xC
  CHECK_EVENTID 24
  BEQ 1 1 0xC
    CHECK_EVENTID 25
	BEQ 1 1 0xC
	  CHECK_EVENTID 26
	  BEQ 1 1 0xC
        CALL EndingScene
LABEL 1
NoFade
ENDA

ArmoryList:
SHLI SteelSword RubySword WindSword Runesword SteelLance SapphireLance Javelin SteelAxe EmeraldAxe HandAxe SteelBow HuntingBow Longbow
ALIGN 4

VendorList:
SHLI Vulnerary PureWater MasterKey Shine Divine Purge Mend Physic StoneStaff PoisonStaff
ALIGN 4

GlobalPostVillageEventCheck:
ENUF VillageJustVisitedFlag

//counter 0 is # of visited villages

COUNTER_CHECK 0

SVAL 1 0
BEQ 29 1 0xC
  SVAL 1 1
  BNE 15 1 0xC
    CALL OneVillageVisitedEvent
  LABEL 15
  SVAL 1 2
  BNE 16 1 0xC
    CALL TwoVillageVisitedEvent
  LABEL 16
  SVAL 1 3
  BNE 17 1 0xC
    CALL ThreeVillageVisitedEvent
  LABEL 17
  SVAL 1 4
  BNE 18 1 0xC
    CALL FourVillageVisitedEvent
  LABEL 18
  SVAL 1 5
  BNE 19 1 0xC
    CALL FiveVillageVisitedEvent
  LABEL 19
  SVAL 1 6
  BNE 20 1 0xC
    CALL SixVillageVisitedEvent
  LABEL 20
  SVAL 1 7
  BNE 21 1 0xC
    CALL SevenVillageVisitedEvent
  LABEL 21
  SVAL 1 8
  BNE 22 1 0xC
    CALL EightVillageVisitedEvent
  LABEL 22
  SVAL 1 9
  BNE 23 1 0xC
    CALL NineVillageVisitedEvent
  LABEL 23
  SVAL 1 10
  BNE 24 1 0xC
    CALL TenVillageVisitedEvent
  LABEL 24
  SVAL 1 11
  BNE 25 1 0xC
    CALL ElevenVillageVisitedEvent
  LABEL 25
  SVAL 1 12
  BNE 26 1 0xC
    CALL TwelveVillageVisitedEvent
  LABEL 26
  SVAL 1 13
  BNE 27 1 0xC
    CALL ThirteenVillageVisitedEvent
  LABEL 27
  SVAL 1 14
  BNE 28 1 0xC
    CALL FourteenVillageVisitedEvent
  LABEL 28
  SVAL 1 15
  BNE 29 1 0xC
    CALL FifteenVillageVisitedEvent
LABEL 29
Text(ErrorText)
NoFade
ENDA

OneVillageVisitedEvent:
LOAD1 0x1 EnemyUnits2
ENUN
Text(Village1Text) //Guy spawns on the map with a few other units and says something
NoFade
ENDB

TwoVillageVisitedEvent:
Text(Village2Text) //Serra complains about how slow this is going
NoFade
ENDB

ThreeVillageVisitedEvent:
//Text(Village3Text) //Random Event
CALL RandomEventHandler
NoFade
ENDB

FourVillageVisitedEvent:
CAM1 [0,12]
LOAD1 0x1 FirstThief
ENUN
//Text(Village4Text) //Spawns a chest-seeking thief in the bottom left with an almost-full inventory (so he goes for the chest closest then skedaddles), then Matthew comments on it
NoFade
ENDB

FirstThief:
UNIT PrologueBandit Thief 0 Level(15,Enemy,True) [0,12] 0x0 0x0 0x0 0x1 [IronSword,Vulnerary,Vulnerary,Vulnerary] StealingThiefAI
UNIT

FiveVillageVisitedEvent:
LOAD1 0x1 EnemyUnits4
ENUN
Text(Village5Text) //Dorcas spawns on the map with a few other units and says something
NoFade
ENDB

SixVillageVisitedEvent:
//Text(Village6Text) //Random Event
CALL RandomEventHandler
NoFade
ENDB

SevenVillageVisitedEvent:
//Text(Village7Text) //Random Event
CALL RandomEventHandler
NoFade
ENDB

EightVillageVisitedEvent:
Text(Village8Text) ////Lucius and Priscilla spawn on the map with a few other units each after a conversation between them and Raven
LOAD1 0x1 EnemyUnits5 //Lucius
ENUN
LOAD1 0x1 EnemyUnits6 //Priscilla
ENUN
NoFade
ENDB

NineVillageVisitedEvent:
//Text(Village9Text) //Random Event
CALL RandomEventHandler
NoFade
ENDB

TenVillageVisitedEvent:
//Text(Village10Text) //Random Event
CALL RandomEventHandler
NoFade
ENDB

ElevenVillageVisitedEvent:
CAM1 [24,12]
LOAD1 0x1 SecondThief
ENUN
//Text(Village11Text) //Spawns a chest-seeking thief from the top right, two free inventory slots (making one chest always guaranteed as these are the only two)

NoFade
ENDB

SecondThief:
UNIT PrologueBandit Thief 0 Level(15,Enemy,True) [24,12] 0x0 0x0 0x0 0x1 [IronSword,Vulnerary,Vulnerary,Vulnerary] StealingThiefAI
UNIT

EscapePointPointerTable(0, ThiefEscapePoints)

ALIGN 4
ThiefEscapePoints:
EscapePoint(0, 12, MoveDown)
EscapePoint(24, 12, MoveDown)
EscapePointEnd

TwelveVillageVisitedEvent:
//Text(Village12Text) //Random Event
CALL RandomEventHandler
NoFade
ENDB

ThirteenVillageVisitedEvent:
Text(Village13Text) //Raven enters the map and says the thanos line
LOAD1 0x1 EnemyUnits //Raven
ENUN
NoFade
ENDB

FourteenVillageVisitedEvent:
Text(Village14Text) //Serra complaining again but now we're almost done
NoFade
ENDB

FifteenVillageVisitedEvent: //Two possible texts based on the status of Raven; if alive, a "now all we have to do is take down Raven" and if dead a "that should be it" followed by chapter ending
CHECK_EVENTID 24
BNE 1 0 0xC
  Text(Village15Text) 
  CAM1 Raven
  CUMO Raven
  STAL 40
  CURE
  GOTO 2
LABEL 1
Text(Village15TextAlt)
LABEL 2
NoFade
ENDB


#define RandomEventFlag1 121
#define RandomEventFlag2 122
#define RandomEventFlag3 123
#define RandomEventFlag4 124
#define RandomEventFlag5 125
#define RandomEventFlag6 126

RandomEventHandler:

LABEL 777

//roll a number 0-5
RANDOMNUMBER 5 //assumedly returns in slot 0xC
SVAL 1 0
BNE 0 1 0xC //if number isn't 0, go to the next check
  //number is 0! is the first random event flag set?
  CHECK_EVENTID RandomEventFlag1
  SVAL 1 1
  BEQ 0 1 0xC //skip calling this random event if so
    CALL RandomEvent1 //this will end internally
LABEL 0

SVAL 1 1
BNE 1 1 0xC //if number isn't 1, go to the next check
  //number is 1! is the second flag set?
  CHECK_EVENTID RandomEventFlag2
  SVAL 1 1
  BEQ 1 1 0xC //skip calling this random event if so
    CALL RandomEvent2 //this will end internally  
LABEL 1

SVAL 1 2
BNE 2 1 0xC //if number isn't 2, go to the next check
  //number is 2! is the third flag set?
  CHECK_EVENTID RandomEventFlag3
  SVAL 1 1
  BEQ 2 1 0xC //skip calling this random event if so
    CALL RandomEvent3 //this will end internally  
LABEL 2

SVAL 1 3
BNE 3 1 0xC //if number isn't 3, go to the next check
  //number is 3, or any prior number if the last random event(s) have already occurred! is the fourth flag set?
  CHECK_EVENTID RandomEventFlag4
  SVAL 1 1
  BEQ 2 1 0xC //skip calling this random event if so
    CALL RandomEvent4 //this will end internally  
LABEL 3

SVAL 1 4
BNE 4 1 0xC //if number isn't 4, go to the next check
  //number is 4, or any prior number if the last random event(s) have already occurred! is the fifth flag set?
  CHECK_EVENTID RandomEventFlag5
  SVAL 1 1
  BEQ 2 1 0xC //skip calling this random event if so
    CALL RandomEvent5 //this will end internally  
LABEL 4
SVAL 1 5
BNE 5 1 0xC //if number isn't 5, go to the next check
//number is 5, or any other number if the last random event(s) have already occurred! is the sixth flag set?
  CHECK_EVENTID RandomEventFlag6
  SVAL 1 1
  BEQ 5 1 0xC //skip calling this random event if so
    CALL RandomEvent6 //this will end internally  
LABEL 5

//if we rerolled the same number, we run this all again
GOTO 777

//this point should never be reachable, so let's give an error message
Text(ErrorText)

NoFade
ENDB

RandomEvent1: //weather picks up (weather swaps from snow to blizzard)
ENUT RandomEventFlag1
WEA1 BlizzardWeather
Text(RandomEvent1Text)
NoFade
ENDB

RandomEvent2: //find some gold
ENUT RandomEventFlag2
Text(RandomEvent2Text)
GiveMoney(10000)
NoFade
ENDB

RandomEvent3: //toggle fog (check RandomEventFlag4's status to determine which)
ENUT RandomEventFlag3
Text(RandomEvent3Text)
//no way to check current fog level, so have to check for the other thing touching fog level
CHECK_EVENTID RandomEventFlag4
SVAL 1 1
BEQ 1 1 0xC
VCWF 4 //Enable Fog
GOTO 2

LABEL 1 //Disable Fog
VCWF 0

LABEL 2
NoFade
ENDB

RandomEvent4: //toggle fog (check RandomEventFlag3's status to determine which)
ENUT RandomEventFlag4
Text(RandomEvent4Text)
//no way to check current fog level, so have to check for the other thing touching fog level
CHECK_EVENTID RandomEventFlag3
SVAL 1 1
BEQ 1 1 0xC
VCWF 4 //Enable Fog
GOTO 2

LABEL 1 //Disable Fog
VCWF 0

LABEL 2
NoFade
ENDB

RandomEvent5: //fiiine reinforcements
ENUT RandomEventFlag5
Text(RandomEvent5Text)
CAM1 [0,12]
LOAD1 0x1 RandomReinforcements1
ENUN
NoFade
ENDB

RandomEvent6: //fiiine reinforcements
ENUT RandomEventFlag6
Text(RandomEvent6Text)
CAM1 [24,12]
LOAD1 0x1 RandomReinforcements2
ENUN
NoFade
ENDB

RandomReinforcements1:
UNIT PrologueBandit Sage 0 Level(3,Enemy,True) [0,12] 0x0 0x0 0x1 REDA1R11 [Bolting] NoAI
UNIT PrologueBandit GreatKnight 0 Level(3,Enemy,True) [0,12] 0x0 0x0 0x1 REDA2R12 [SteelSword] NoAI
UNIT

RandomReinforcements2:
UNIT PrologueBandit Bishop 0 Level(3,Enemy,True) [24,12] 0x0 0x0 0x1 REDA22R11 [Physic] HealUnits
UNIT PrologueBandit Paladin 0 Level(3,Enemy,True) [24,12] 0x0 0x0 0x1 REDA21R12 [Javelin] NoAI
UNIT

//scoring system

CalculateScore:

//rated on:
//tactics - turn count
//survival - base score for keeping everyone alive, -1 per unit death (use the EventSurvivalGetter ASMC for this)
//funds - cost of all items owned + gold (use the EventFundsGetter ASMC for this)

//in these 3 categories you get a score each and then one overall
//0=E 1=D 2=C 3=B 4=A 5=S
//EventFundsGetter will return the rank value (set in chapter data the comparative value on this)

//turn count ranks are 5 S, 6-7 A, 8 B, 9 C, 10 D, 11+ E

//each of these categories are at max 5, meaning all 3 fit in a counter
//so, counter ranges for overall ranking:
//S - 15
//A - 11-14
//B - 8-10
//C - 5-9
//D - 2-4
//E - 0-1

//you really have to go out of your way to rank lower than C


//there's no way to add more than 1 to a counter at a time
//let's use counter 1 for this

COUNTER_SET 1 0 //overall ranking
COUNTER_SET 5 0 //tactics ranking
COUNTER_SET 3 0 //survival ranking
COUNTER_SET 4 0 //funds ranking

CHECK_TURNS 

//0xC = turn count

SVAL 1 8
BGT 1 0xC 1
  COUNTER_INC 1
  COUNTER_INC 5
LABEL 1



SVAL 1 9
BGT 2 0xC 1
  COUNTER_INC 1
  COUNTER_INC 5
LABEL 2



SVAL 1 0
BGT 3 0xC 1
  COUNTER_INC 1
  COUNTER_INC 5
LABEL 3



SVAL 1 12
BGT 4 0xC 1
  COUNTER_INC 1
  COUNTER_INC 5
LABEL 4



SVAL 1 14
BGT 5 0xC 1
  COUNTER_INC 1
  COUNTER_INC 5
LABEL 5



ASMC EventSurvivalGetter|1

//mem slot 1 = ranking

//no way to add a value in a slot to a counter so doing it the hard way

SVAL 2 5
BLT 6 1 2
  COUNTER_INC 1
  COUNTER_INC 3
LABEL 6

SVAL 2 4
BLT 7 1 2
  COUNTER_INC 1
  COUNTER_INC 3
LABEL 7

SVAL 2 3
BLT 8 1 2
  COUNTER_INC 1
  COUNTER_INC 3
LABEL 8

SVAL 2 2
BLT 9 1 2
  COUNTER_INC 1
  COUNTER_INC 3
LABEL 9

SVAL 2 1
BLT 10 1 2
  COUNTER_INC 1
  COUNTER_INC 3
LABEL 10


ASMC EventFundsGetter|1

//mem slot 1 = funds

//no way to add a value in a slot to a counter so doing it the hard way

SVAL 2 5
BLT 11 1 2
  COUNTER_INC 1
  COUNTER_INC 4
LABEL 11

SVAL 2 4
BLT 12 1 2
  COUNTER_INC 1
  COUNTER_INC 4
LABEL 12

SVAL 2 3
BLT 13 1 2
  COUNTER_INC 1
  COUNTER_INC 4
LABEL 13

SVAL 2 2
BLT 14 1 2
  COUNTER_INC 1
  COUNTER_INC 4
LABEL 14

SVAL 2 1
BLT 15 1 2
  COUNTER_INC 1
  COUNTER_INC 4
LABEL 15

//counter now contains overall score


//tactics ranking

COUNTER_CHECK 5

SVAL 1 5
BNE 22 1 0xC
  Text(TextTacticsRatingS)
  GOTO 27
LABEL 22

SVAL 1 4
BNE 23 1 0xC
  Text(TextTacticsRatingA)
  GOTO 27
LABEL 23

SVAL 1 3
BNE 24 1 0xC
  Text(TextTacticsRatingB)
  GOTO 27
LABEL 24

SVAL 1 2
BNE 25 1 0xC
  Text(TextTacticsRatingC)
  GOTO 27
LABEL 25

SVAL 1 1
BNE 26 1 0xC
  Text(TextTacticsRatingD)
  GOTO 27
LABEL 26

Text(TextTacticsRatingE)

LABEL 27



//survival ranking

COUNTER_CHECK 3

SVAL 1 5
BNE 28 1 0xC
  Text(TextSurvivalRatingS)
  GOTO 33
LABEL 28

SVAL 1 4
BNE 29 1 0xC
  Text(TextSurvivalRatingA)
  GOTO 33
LABEL 29

SVAL 1 3
BNE 30 1 0xC
  Text(TextSurvivalRatingB)
  GOTO 33
LABEL 30

SVAL 1 2
BNE 31 1 0xC
  Text(TextSurvivalRatingC)
  GOTO 33
LABEL 31

SVAL 1 1
BNE 32 1 0xC
  Text(TextSurvivalRatingD)
  GOTO 33
LABEL 32

Text(TextSurvivalRatingE)

LABEL 33


//funds ranking

COUNTER_CHECK 4
SVAL 1 5
BNE 34 1 0xC
  Text(TextFundsRatingS)
  GOTO 39
LABEL 34

SVAL 1 4
BNE 35 1 0xC
  Text(TextFundsRatingA)
  GOTO 39
LABEL 35

SVAL 1 3
BNE 36 1 0xC
  Text(TextFundsRatingB)
  GOTO 39
LABEL 36

SVAL 1 2
BNE 37 1 0xC
  Text(TextFundsRatingC)
  GOTO 39
LABEL 37

SVAL 1 1
BNE 38 1 0xC
  Text(TextFundsRatingD)
  GOTO 39
LABEL 38

Text(TextFundsRatingE)

LABEL 39


//final ranking

COUNTER_INC 1
COUNTER_INC 1
COUNTER_INC 1

COUNTER_CHECK 1
SVAL 1 15
BLT 16 0xC 1
    MUSC 61
	Text(TextOverallRatingS)
	GOTO 21
LABEL 16

SVAL 1 11
BLT 17 0xC 1
	MUSC 61
	Text(TextOverallRatingA)
	GOTO 21
LABEL 17

SVAL 1 8
BLT 18 0xC 1
	MUSC 60
	Text(TextOverallRatingB)
	GOTO 21
LABEL 18

SVAL 1 5
BLT 19 0xC 1
	MUSC 60
	Text(TextOverallRatingC)
	GOTO 21
LABEL 19

SVAL 1 2
BLT 20 0xC 1
	MUSC 58
	Text(TextOverallRatingD)
	GOTO 21
LABEL 20

MUSC 58
Text(TextOverallRatingE)

LABEL 21

NoFade
ENDA


RandomVillageTextEvent:

RANDOMNUMBER 2

SVAL 1 2
BNE 1 1 0xC
  Text(VisitVillageText1)
  GOTO 3
LABEL 1
SVAL 1 1
BNE 2 1 0xC
  Text(VisitVillageText2)
  GOTO 3
LABEL 2
Text(VisitVillageText3)
LABEL 3
NoFade
ENDA


